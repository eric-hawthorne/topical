origin   everybitcounts.net2006
artifact topical
package  web

"""
 dialog.rel

 Web request handlers.
"""

import
   model
   view
   strings
   http_srv   


index
   request http_srv.Request 
   ...kw {} String > String 
> 
   String 
   Any
"""
 Handles a request for the root (i.e. empty path) url on the server and port.
 Displays a user login page, which may or may not have a designated next URL path to
 continue to after the user logs in. This is determined by whether the "?next=" parameter
 is set on the request for the root url.
"""  

   tok = session request
   nextUrl nextFound = kw["next"]
   if not nextFound
      mextUrl = ""

   notice = ""

   tok = session request
   if tok
      if exists tok
         visit = summon tok
         notice = getNotice visit
      else
         notice = "Your login session has expired. Please log in."

   => "index.html"
      {
         "next"   => nextUrl
         "notice" => notice
      }String > Any


listOntologies request http_srv.Request username String > String String Any
"""
 Display a list of the ontologies created by or viewable by the user.
 Also displays a form to create a new ontology.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok (cat "/?next=/listOntologies/" username)
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage

   if not exists username
      => errPage fill "Error: User '%s' not found." username
                 "Ok"       
                 "/"
   user = summon username 

   visibleOntologies = []model.Ontology
   for ontology in user.ontologies
      if hasPermission visit.user "view" ontology
         visibleOntologies += ontology
   
   => header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""
      "listOntologies.html"
      {
         "ontologies" => visibleOntologies
         "tok"        => tok
         "username"   => username
      }String > Any


// icon > String String
// """
//  Serves the favicon.ico request.
// """
//    => "IMAGE FILE"
//       "favicon.png"


// default orgId String ...kw {} String > String ...args [] String > String Any


default 
   request http_srv.Request 
   ...kw {} String > String
   ...args [] String
>
   String 
   Any
   Any
"""
 Handles all url paths on this server and port which are not otherwise handled.

 Permission cases:

 If no token, then user = model.ensureGuestUser
 else 
   visit err = ensureLoggedInUser tok
   if err 
       goToLoginPage maybe visit.user err next = topic or ontologyUrl
    else
       viewable = hasPermission user  "view" topic.ontology
       if not viewa
       editable = hasPermission user  "edit" topic.ontology

"""
   // tok = kw[! "tok"]
   tok = session request 
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  
   
   username = args[0] 

   if eq (len args) 1 
      => extendSession
         "REDIRECT"
         cat "/listOntologies/" username

   ontologyUrlPath = cat "/" username "/" (urlPathPartEncode args[1] false)

   if exists ontologyUrlPath
      if eq (len args) 2
         ontology = summon ontologyUrlPath
         => extendSession
            "REDIRECT"
            cat ontologyUrlPath "/" ontology.root.urlPathComponent
      else            
         topicUrlPathComponent = urlPathPartEncode args[2] false
         topicUrlPath = cat ontologyUrlPath "/" topicUrlPathComponent
         if exists topicUrlPath
            topic = summon topicUrlPath

            if eq (len args) 4
               activeParentUrlPathComponent = args[3]
               activeParentUrlPath = cat ontologyUrlPath "/" activeParentUrlPathComponent
               if exists activeParentUrlPath
                  topic.activeParent = summon activeParentUrlPath
               else
                  => errPage extendSession
                             fill "Error: Unknown topic '%s'" activeParentUrlPath
                             "Ok"       
                             ontologyUrlPath
               => extendSession
                  "REDIRECT"
                  topicUrlPath
 
            ensureActiveParent topic

            visit err = ensureHavePermission tok "view" topic.ontology 

            if err
               => extendSession
                  "REDIRECT"
                  cat "/?next=" topicUrlPath

            editable = hasPermission visit.user "edit" topic.ontology


            => extendSession
               "topic.html" 
               {
                  "topic"    => topic
                  "editable" => editable
                  "tok"      => visit.token
               }String > Any
         else
            => errPage extendSession
                       fill "Error: Unknown topic '%s'" topicUrlPath
                       "Ok"       
                       ontologyUrlPath
   else
      if eq (len args) 2
         => extendSession
            "REDIRECT"
            cat "/create" ontologyUrlPath 
      else
         => errPage extendSession
                    fill "Error: Unknown ontology '%s'" ontologyUrlPath
                    "Ok"       
                    cat "/" username 



createOntology request http_srv.Request username String name String > String String Any
"""
 Processes the form for creating a new ontology on the list ontologies page.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok "/"
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   ontologyUrlPath = cat "/" username "/" (urlPathPartEncode name)
   => extendSession
      "REDIRECT"
      cat "/create" ontologyUrlPath 



create request http_srv.Request username String ontologyUrlPath String > String String Any
"""
 Confirms that the user wishes to create a new ontology with the given name.
 Also verifies, again, that such an ontology does not already exist.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok "/"
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   ontologyUrlPath = cat "/" username "/" ontologyUrlPath

   // print "ontologyUrlPath" ontologyUrlPath
   // print "name" (ontologyNameFormat ontologyUrlPath)
   // print "uns" (unsanitize ontologyUrlPath)

   => extendSession
      "create.html"
      {
         "urlPath" => ontologyUrlPath
         "name"    => ontologyNameFormat ontologyUrlPath
         "tok"     => tok
      }String > String


createAction request http_srv.Request ontologyUrlPath String rootTopicTitle String action String > String String Any
"""
 Actually creates a new ontology, after verifying that the ontology name does not already exist.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok "/"
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   i = lastIndex ontologyUrlPath "/"  
   username = slice ontologyUrlPath 1 i

   if eq action "Cancel"
      => extendSession
         "REDIRECT"
         cat "/" username 

   if exists ontologyUrlPath
      => errPage extendSession
                 fill "Error: Ontology '%s' already exists." ontologyUrlPath
                 "Ok"       
                 cat "/" username

   if not exists username
      => errPage extendSession
                 fill "Error: User '%s' not found." username
                 "Ok"       
                 "/"
   user = summon username

   topic = model.Topic
   topic.title = rootTopicTitle
   topic.urlPathComponent = urlSanitize rootTopicTitle
   topic.content = cat "<h1>" rootTopicTitle "</h1>"

   ontology = model.Ontology
   ontology.name = ontologyNameFormat ontologyUrlPath
   ontology.urlPathComponent = ontologyUrlPath
   ontology.description = ""
   ontology.user = user

   grantPermission user "view" ontology
   grantPermission user "edit" ontology
   grantPermission user "create" ontology
   grantPermission user "delete" ontology
   grantPermission user "rename" ontology

   ontology.root = topic
   topic.level = 0  // I am the root topic
   topic.ontology = ontology

   dub ontology ontologyUrlPath

   topicUrlPath = urlPath topic

   dub topic topicUrlPath

   => extendSession
      "REDIRECT"
      topicUrlPath


addParent request http_srv.Request topic String > String String Any
"""
 Display a dialog asking user to either search for a topic to be the parent,
 or create a new topic to be the parent.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   childTopic = summon topic
   => extendSession
      "addParentOrChild.html"
      {"url"=>topic "title"=>childTopic.title "rel"=>"parent" "relCap"=>"Parent"}String > String
      

addChild request http_srv.Request topic String > String String Any
"""
 Display a dialog asking user to either search for a topic to be the child,
 or create a new topic to be the child.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic     
                 "Ok"       
                 "/"
   parentTopic = summon topic
   => extendSession
      "addParentOrChild.html"
      {"url"=>topic "title"=>parentTopic.title "rel"=>"child" "relCap"=>"Child"}String > String


addTo request http_srv.Request topic String > String String Any
"""
 Display a dialog asking user to either search for a topic to be the target of a relationship arrow
 or create a new topic to be the target.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   fromTopic = summon topic
   => extendSession
      "addToOrFrom.html"
      {"url"=>topic "title"=>fromTopic.title "rel"=>"related-to" "relCap"=>"Related-To"}String > String



addFrom request http_srv.Request topic String > String String Any
"""
 Display a dialog asking user to either search for a topic to be the source of a relationship arrow
 or create a new topic to be the source.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   toTopic = summon topic
   => extendSession
      "addToOrFrom.html"
      {"url"=>topic "title"=>toTopic.title "rel"=>"related-from" "relCap"=>"Related-From"}String > String


addSibling request http_srv.Request topic String > String String Any
"""
 Display a dialog asking user to either search for a topic to be a sibling of the topic under its
 active parent.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   currentChildTopic = summon topic
   => extendSession
      "addSibling.html"
      {
         "url"    => topic 
         "title"  => currentChildTopic.title 
         "parent" => currentChildTopic.activeParent
      }String > Any



linkNewTopicAction request http_srv.Request linkType String linkSubtype String topic String title String 
> String String Any
"""
 Create a new topic, and establish it in a particular type of linked relationship with
 the original topic. Then display the new topic in the topic display UI.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   title = trimSpace title
   if not title
      => errPage extendSession
                 "Error: New topic must be given a title." 
                 "Ok"       
                 topic 

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   fromTopic = summon topic
   fromLevel = fromTopic.level

   visit err = ensureHavePermission tok "edit" fromTopic.ontology
   if err
      => extendSession
         "REDIRECT" 
         cat "/?next=" topic 

   newTopic = model.Topic
   newTopic.title = title
   newTopic.urlPathComponent = urlSanitize title
   newTopic.content = cat "<h1>" title "</h1>"
   

   newTopic.ontology = fromTopic.ontology
   
   topicUrlPath = urlPath newTopic
   
   if exists topicUrlPath
      => errPage extendSession
                 fill "Error: Topic '%s' already exists." title
                 "Ok"       
                 topic 

   dub newTopic topicUrlPath

   // Now link to the new topic from the old one.

   if eq linkType "parent"
      assertParentChild newTopic fromTopic linkSubtype
      newTopic.level = minus fromLevel 1
      fromTopic.activeParent = newTopic
   elif eq linkType "related-to"
      assertRelated fromTopic newTopic
      newTopic.level = fromLevel
   elif eq linkType "related-from"
      assertRelated newTopic fromTopic  // the relation is from newTopic to current topic
      newTopic.level = fromLevel
   elif eq linkType "child"
      assertParentChild fromTopic newTopic linkSubtype
      newTopic.level = plus fromLevel 1
      newTopic.activeParent = fromTopic
   else  // must be a sibling
      parent = fromTopic.activeParent
      assertParentChild parent newTopic linkSubtype
      newTopic.level = plus parent.level 1 
      newTopic.activeParent = parent     
      
   => extendSession
      "REDIRECT"
      topicUrlPath 


topicSearch request http_srv.Request next String rel String linkSubtype String topic String key String 
> String String Any
"""
 Displays a page containing a list of topics whose title contains the key.
 TODO extend to searching content.  
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   key = trimSpace key
   if lt (len key) 2
      => errPage extendSession
                 "Please specify a longer keyword to search for topics."
                 "Ok"       
                 topic
   // print "got to 1"
   topics1 = []model.Topic cat "title like '%" key "%' order by title"
   // print "got to 2"
   if lt (len topics1) 30
      // print "got to 3"
      topics2 = []model.Topic cat "content like '%" key "%' order by title"
      // print "got to 4"
      topics3 = []model.Topic
      for topic2 in topics2
         // print "got to 5"
         if not contains topics1 topic2 
            topics3 += topic2
         // print "got to 6"
      for topic3 in topics3
         // print "got to 7"
         topics1 += topic3
      // print "got to 8"
   if not topics1
      => errPage extendSession
                 "No topics match your search key."
                 "Ok"  
                 topic  
   => extendSession
      "chooseTopic.html"
      {
         "topics"      => topics1
         "next"        => next
         "rel"         => rel
         "linkSubtype" => linkSubtype
         "topic"       => topic 
      }String > Any         



linkExisting request http_srv.Request topic String chosen String rel String linkSubtype String > String String Any
"""
 Link the chosen topic as a parent of the topic.
 Then return to display the original topic.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   if not exists chosen
      => errPage extendSession
                 fill "Error: Topic '%s' not found." chosen
                 "Ok"       
                 "/"
   fromTopic = summon topic
   chosenTopic = summon chosen

   visit err = ensureHavePermission tok "edit" fromTopic.ontology
   if err
      => extendSession
         "REDIRECT" 
         cat "/?next=" topic

   // Now link to the new topic from the old one.

   if eq rel "parent"
      assertParentChild chosenTopic fromTopic linkSubtype
      fromTopic.activeParent = chosenTopic
      possibleNewLevel = plus 1 chosenTopic.level
      if lt possibleNewLevel fromTopic.level
         fromTopic.level = possibleNewLevel
   elif eq rel "related-to"
      assertRelated fromTopic chosenTopic
   elif eq rel "related-from"
      assertRelated chosenTopic fromTopic  // the relation is from chosenTopic to current topic
   elif eq rel "child"
      assertParentChild fromTopic chosenTopic linkSubtype
      chosenTopic.activeParent = fromTopic
      possibleNewLevel = plus 1 fromTopic.level
      if lt possibleNewLevel chosenTopic.level
         chosenTopic.level = possibleNewLevel
   else  // must be a sibling
      parent = fromTopic.activeParent
      if eq chosenTopic fromTopic
         => errPage extendSession
                    "Cannot choose self as a sibling."
                    "Ok"  
                    topic
      assertParentChild parent chosenTopic linkSubtype
      chosenTopic.activeParent = parent  
      possibleNewLevel = plus 1 parent.level
      if lt possibleNewLevel chosenTopic.level
         chosenTopic.level = possibleNewLevel

   => extendSession
      "REDIRECT"
      topic


saveTopicAction request http_srv.Request topic String content String > String String Int
"""
 Saves edited content of the topic.

 TODO - Fix up what to do if not logged in. This is called by ajax I  think.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      print "saveTopicAction no visit" 
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      print "saveTopicAction non-logged-in visit"    
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   editedTopic = summon topic
   content = stripNbsps content
   editedTopic.content = content
   => extendSession "HTTP CODE" 200


saveAndRenameTopicAction request http_srv.Request topic String content String newTitle String > String String String
"""
 Saves edited content of the topic, and also renames the topic.

 TODO - Fix up what to do if not logged in. This is called by ajax I  think.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      print "saveAndRenameTopicAction no visit" 
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      print "saveAndRenameTopicAction non-logged-in visit"    
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   editedTopic = summon topic

   visit err = ensureHavePermission tok "edit" editedTopic.ontology
   if err
      => extendSession
         "REDIRECT" 
         cat "/?next=" topic

   content = stripNbsps content
   newTitle = stripNbsps newTitle
   editedTopic.content = content
   editedTopic.title = newTitle
   editedTopic.urlPathComponent = urlSanitize newTitle
   editedTopicUrlPath = urlPath editedTopic
   rename topic editedTopicUrlPath
   => extendSession
      "REDIRECT"
      editedTopicUrlPath


topicDelete request http_srv.Request topic String > String String Any
"""
 Display a dialog seeking confirmation of deleting a topic.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn 
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   topicToDelete = summon topic
   => extendSession
      "deleteTopic.html"
      {
         "topic" => topicToDelete 
      }String > Any


topicDeleteAction request http_srv.Request topic String action String > String String String
"""
 Actually delete a topic, unless cancelled, in which case redirect to display the topic.
 If deletes the topic, redirects to display the root topic of the ontology.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if eq action "Cancel"
      => extendSession
         "REDIRECT"
         topic

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   topicToDelete = summon topic
   ontology = topicToDelete.ontology
   extricate topicToDelete
   delete topicToDelete
   => extendSession
      "REDIRECT"
      ontology.urlPathComponent
   

editChildren request http_srv.Request topic String > String String Any
"""
 Re-order children in the list of children.
 Or remove a child.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   t = summon topic
   => extendSession
      "editChildren.html"
      {
         "topic" => t
      }String > Any



editChildrenAction request http_srv.Request topic String ordinal Int action String > String String Any
"""
 Move a child up or down in the child list, or remove it as a child.
 Redirect to the edit children page. 
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   tpc = summon topic
   if eq action " ^ " 
      swap tpc.childLinks ordinal (minus ordinal 1)
           
   elif eq action " v " 
      swap tpc.childLinks ordinal (plus ordinal 1)  
   else  // "-x-"
      childLink = tpc.childLinks[ordinal]
      tpc.childLinks -= childLink
      childLink.child.parentLinks -= childLink

   => extendSession
      "REDIRECT"
      cat "editChildren?topic=" topic



graph request http_srv.Request topic String > String String Any
"""
 Display a topic association graph centred on the current topic.
"""
   tok = session request
   visit loggedIn loginPage = ensureLoggedIn tok topic
   if not visit
      => "REDIRECT"
         loginPage
         nil
   elif not loggedIn
      => header setCookie cookie request "SESS"
         "REDIRECT"
         loginPage   
   extendSession = header setCookie "SESS" tok view.ADMIN_SESSION_TIMEOUT "/" ""  

   if not exists topic
      => errPage extendSession
                 fill "Error: Topic '%s' not found." topic
                 "Ok"       
                 "/"
   tpc = summon topic

   visit err = ensureHavePermission tok "view" tpc.ontology 

   if err
      => extendSession
         "REDIRECT"
         cat "/?next=" topic 

   // compute the positions of the topic for this user
   width height = setGraphPositions tpc visit.user

   => extendSession
      "graph.html"
      {
         "topic"  => tpc
         "user"   => visit.user
         "width"  => width
         "height" => height
      }String > Any
